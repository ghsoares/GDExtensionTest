[gd_resource type="ShaderMaterial" load_steps=2 format=2]

[sub_resource type="Shader" id=1]
code = "shader_type canvas_item;

uniform sampler2D ditheringTexture;
uniform float ditheringInfluence = .1;

uniform sampler2D fogNoise;
uniform float fogTiling = 512.0;

uniform vec2 fogMotion;
uniform float windSpeed;

uniform int fogOctaves = 2;
uniform float fogLacunarity = 2.0;
uniform float fogPersistance = .25;

uniform sampler2D heightSubtractCurve;
uniform float fogSteps = 8.0;

uniform sampler2D fogGradient;

varying vec2 v;
varying float time;

float Ease(float x, float c) {return x;}

void vertex() {
	v = VERTEX;
	time = TIME;
}

float GetNoise() {
	float n = 0.0;
	
	int oct = max(fogOctaves, 1);
	
	vec2 motion = fogMotion;
	motion.x += windSpeed;
	
	float period = fogTiling;
	float pers = 1.0;
	
	float total = 0.0;
	
	for (int i = 0; i < oct; i++) {
		vec2 uv = v / period;
		uv -= (motion / fogTiling) * time;
		n += texture(fogNoise, uv).r * pers;
		
		total += pers;
		
		period /= fogLacunarity;
		pers *= fogPersistance;
	}
	
	return n / max(total, 1.0);
}

void fragment() {
	float n = GetNoise();
	n = Ease(n, -2.0);
	
	n -= texture(heightSubtractCurve, vec2(UV.y, 0.0)).r;
	
	vec2 ditherSize = vec2(textureSize(ditheringTexture, 0));
	float d = texture(ditheringTexture, v / ditherSize).r * 2.0 - 1.0;
	
	n += d * ditheringInfluence;
	
	n = floor(n * fogSteps) / fogSteps;
	n = clamp(n, 0.0, 1.0);
	
	vec4 col = texture(fogGradient, vec2(n, 0.0));
	
	COLOR = col;
}







"

[resource]
shader = SubResource( 1 )
shader_param/ditheringInfluence = 0.1
shader_param/fogTiling = 512.0
shader_param/fogMotion = null
shader_param/windSpeed = null
shader_param/fogOctaves = 2
shader_param/fogLacunarity = 2.0
shader_param/fogPersistance = 0.25
shader_param/fogSteps = 8.0
